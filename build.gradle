subprojects {

  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  group 'me.yushust.inject'
  version '0.4.2-SNAPSHOT'

  publishing {
    repositories {
      maven {
        def snapshot = version.toString().endsWith("-SNAPSHOT")
        def typeName = snapshot ? 'snapshots' : 'releases'
        def repoUrl = 'https://repo.unnamed.team/repository/unnamed-' + typeName

        name = 'unnamed-' + (snapshot ? 'snapshots' : 'releases')
        url = repoUrl

        credentials {
          username = System.getenv('REPO_USER')
          password = System.getenv('REPO_PASSWORD')
        }
      }
    }
    publications {
      trew(MavenPublication) {
        from components.java
      }
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  test {
    useJUnitPlatform()
  }

}
